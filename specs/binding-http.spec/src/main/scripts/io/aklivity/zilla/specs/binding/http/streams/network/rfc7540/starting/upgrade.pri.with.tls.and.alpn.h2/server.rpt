#
# Copyright 2021-2022 Aklivity Inc.
#
# Aklivity licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

accept "zilla://streams/net0"
       option zilla:window 8192
       option zilla:transmission "duplex"
accepted

read zilla:begin.ext ${proxy:matchBeginEx()
                            .typeId(zilla:id("proxy"))
                            .info()
                              .alpn("h2")
                              .secure()
                                .version("TLSv1.3")
                                .build()
                              .build()
                            .build()}

write zilla:begin.ext ${proxy:beginEx()
                             .typeId(zilla:id("proxy"))
                             .info()
                               .alpn("h2")
                               .secure()
                                 .version("TLSv1.3")
                                 .build()
                               .build()
                             .build()}

connected

read "PRI * HTTP/2.0\r\n"
     "\r\n"
     "SM\r\n"
     "\r\n"

read  [0x00 0x00 0x0c]                   # length = 12
      [0x04]                             # HTTP2 SETTINGS frame
      [0x00]                             # flags = 0x00
      [0x00 0x00 0x00 0x00]              # stream_id = 0
      [0x00 0x03 0x00 0x00 0x00 0x64]    # SETTINGS_MAX_CONCURRENT_STREAMS(0x03) = 100
      [0x00 0x04 0x00 0x00 0x00 0x3c]    # SETTINGS_INITIAL_WINDOW_SIZE(0x04) = 60

write [0x00 0x00 0x00]                  # length = 0
      [0x04]                            # HTTP2 SETTINGS frame
      [0x01]                            # ACK
      [0x00 0x00 0x00 0x00]             # stream_id = 0
write flush

read [0x00 0x00 0x13]                   # length = 19
     [0x01]                             # HEADERS frame
     [0x04]                             # END_HEADERS
     [0x00 0x00 0x00 0x01]              # stream_id = 1
     [0x83]                             # :method: POST
     [0x86]                             # :scheme: http
     [0x84]                             # :path: /
     [0x01] [0x0e] "localhost:8080"     # :authority: localhost:8080

read [0x00 0x00 0x0c]                   # length = 12
     [0x00]                             # HTTP2 DATA frame
     [0x00]                             # no flags
     [0x00 0x00 0x00 0x01]              # stream_id = 1
     "Hello, world"

read [0x00 0x00 0x00]                   # length = 0
     [0x04]                             # HTTP2 SETTINGS frame
     [0x01]                             # ACK
     [0x00 0x00 0x00 0x00]              # stream_id = 0

# connection-level flow control
write [0x00 0x00 0x04]                                      # length
      [0x08]                                                # WINDOW_UPDATE frame
      [0x00]                                                # no flags
      [0x00 0x00 0x00 0x00]                                 # stream_id=0
      [0x00 0x00 0x20 0x00]                                 # window size increment = 8192
# stream-level flow control
write [0x00 0x00 0x04]                                      # length
      [0x08]                                                # WINDOW_UPDATE frame
      [0x00]                                                # no flags
      [0x00 0x00 0x00 0x01]                                 # stream_id=1
      [0x00 0x00 0x20 0x00]                                 # window size increment = 8192
write flush

# connection-level flow control
write [0x00 0x00 0x04]                                      # length
      [0x08]                                                # WINDOW_UPDATE frame
      [0x00]                                                # no flags
      [0x00 0x00 0x00 0x00]                                 # stream_id=0
      [0x00 0x00 0x00 0x0c]                                 # window size increment = 12
# stream-level flow control
write [0x00 0x00 0x04]                                      # length
      [0x08]                                                # WINDOW_UPDATE frame
      [0x00]                                                # no flags
      [0x00 0x00 0x00 0x01]                                 # stream_id=1
      [0x00 0x00 0x00 0x0c]                                 # window size increment = 12
write flush

read [0x00 0x00 0x00]                  # length = 0
     [0x00]                            # HTTP2 DATA frame
     [0x01]                            # END_STREAM
     [0x00 0x00 0x00 0x01]             # stream_id = 1
